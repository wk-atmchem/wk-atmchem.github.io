#!/bin/csh -f

# ======================= BCONv5.2 Run Script ======================== 
# Usage: run.bcon.csh >&! bcon_v52.log &                                
#
# To report problems or request help with this script/program:        
#             http://www.cmascenter.org
# ==================================================================== 

# ==================================================================
#> Runtime Environment Options
# ==================================================================

#> Choose compiler and set up CMAQ environment with correct 
#> libraries using config.cmaq. Options: intel | gcc | pgi
 setenv compiler intel 
 setenv compilerVrsn 15.0

#> Source the config_cmaq file to set the run environment
 pushd ../../../
 source ./config_cmaq.csh
 popd

#> Check that CMAQ_DATA is set:
 if ( ! -e $CMAQ_DATA ) then
    echo "   $CMAQ_DATA path does not exist"
    exit 1
 endif
 echo " "; echo " Input data path, CMAQ_DATA set to $CMAQ_DATA"; echo " "

#> Set General Parameters for Configuring the Simulation
 set VRSN     = v52                     #> Code Version
 set year     = 2016
 set month    = "01"
 set case     = BASE
 set APPL     = ${year}-${month}-${case}  #> Application Name
 set INPT     = m3conc                 #> Input data type: profile or m3conc?
 set MECH     = cb05e51_ae6_aq          #> Mechanism ID

#> Set the build directory (this is where the CMAQ executable
#> is located by default).
 set BLD      = ${CMAQ_HOME}/PREP/bcon/scripts/BLD_BCON_${VRSN}_${INPT}_${compiler}
 set EXEC     = BCON_${VRSN}_$INPT.exe  
 cat $BLD/BCON_${VRSN}_$INPT.cfg; echo " "; set echo

#> Horizontal grid definition 
 setenv GRID_NAME M_03_08CHINA               #> check GRIDDESC file for GRID_NAME options
 setenv GRIDDESC /GFPS8p/xyyang/wk/CMAQ-5.2.1/data/mcip/2016-01-BASE-sichuan/GRIDDESC #> grid description file 
 setenv IOAPI_ISPH 20                     #> GCTP spheroid, use 20 for WRF-based modeling

#> Vertical layer definition
# setenv LAYER_FILE $CMAQ_DATA/$APPL/met/mcip/METCRO3D_110701.nc #>METCRO3D file from MCIP
 setenv LAYER_FILE /GFPS8p/xyyang/wk/CMAQ-5.2.1/data/mcip/2016-01-BASE-sichuan/METCRO3D_2016107.nc
#> I/O Controls
 setenv IOAPI_LOG_WRITE F     #> turn on excess WRITE3 logging [ options: T | F ]
 setenv IOAPI_OFFSET_64 NO    #> support large timestep records (>2GB/timestep record) [ options: YES | NO ]
 setenv EXECUTION_ID $EXEC    #> define the model execution id

# =====================================================================
#> BCON Configuration Options
#
# BCON can be run in one of two modes:                                     
#     1) use default profile inputs (BC = profile)
#     2) use CMAQ CTM concentration files for nested runs (BC = m3conc)     
# =====================================================================

 set BC = m3conc      #> either profile or m3conc 
# set DATE = "2020-05-11" #> only needed for nested runs
# set YYYYJJJ  = `date -ud "${DATE}" +%Y%j`   #> Convert YYYY-MM-DD to YYYYJJJ
# set YYMMDD   = `date -ud "${DATE}" +%y%m%d` #> Convert YYYY-MM-DD to YYMMDD
# set YYYYMMDD = `date -ud "${DATE}" +%Y%m%d` #> Convert YYYY-MM-DD to YYYYMMDD

 # Set up time and date to process
 set START_DATE = "2016-04-01"
 set END_DATE   = "2016-08-31"
 set TODAYG = ${START_DATE}
 set TODAYJ = `date -ud "${START_DATE}" +%Y%j` #> Convert YYYY-MM-DD to YYYYJJJ
 set STOP_DAY = `date -ud "${END_DATE}" +%Y%j` #> Convert YYYY-MM-DD to YYYYJJJ

while ($TODAYJ <= $STOP_DAY )

 set YYYYJJJ  = `date -ud "${TODAYG}" +%Y%j`   #> Convert YYYY-MM-DD to YYYYJJJ
 set YYMMDD   = `date -ud "${TODAYG}" +%y%m%d` #> Convert YYYY-MM-DD to YYMMDD
 set YYYYMMDD = `date -ud "${TODAYG}" +%Y%m%d` #> Convert YYYY-MM-DD to YYYYMMDD

echo $TODAYJ
# =====================================================================
#> Input/Output Directories
# =====================================================================

 set OUTDIR   = $CMAQ_DATA/bcon       #> output file directory
 

# =====================================================================
#> Input Files
#  
#  Profile Mode (BC = profile)
#     BC_PROFILE = static/default BC profiles 
#  Nesting mode (BC = m3conc)
#     CTM_CONC_1 = the CTM concentration file for the coarse domain          
#     MET_CRO_3D_CRS = the MET_CRO_3D met file for the coarse domain
#                  only set if  or if the vertical grid type is   
#                  changed between nests                                     
#     MET_CRO_3D_FIN = the MET_CRO_3D met file for the inner, nested, domain 
#                  only set if the vertical grid type is changed between  
#                  nests                                                     
#                                                                            
# NOTE: If SDATE (yyyyddd), STIME (hhmmss) and RUNLEN (hhmmss) are not set,  
#       these variables will be set from the input CTM_CONC_1 file           
# =====================================================================
 
 if ( $BC == profile ) then
    setenv BC_PROFILE      $BLD/bc_profile_CB05.dat
 endif

 if ( $BC == m3conc ) then 
    setenv CTM_CONC_1 /GFPS8p/xyyang/wk/CMAQ-5.2.1/data/output_CCTM_cn272016wholeyear/CCTM_CONC_2016-01-BASE_${YYYYMMDD}.nc
    setenv MET_CRO_3D_CRS /GFPS8p/xyyang/wk/CMAQ-5.2.1/data/mcip/2016-BASE/METCRO3D_${YYYYJJJ}.nc
    setenv MET_BDY_3D_FIN /GFPS8p/xyyang/wk/CMAQ-5.2.1/data/mcip/2016-01-BASE-sichuan/METBDY3D_${YYYYJJJ}.nc
#    setenv BNDY_CONC_1    "$OUTDIR/BCON_${VRSN}_${APPL}_${BCON_TYPE}_${YYYYMMDD} -v"
#    setenv CTM_CONC_1 $CMAQ_DATA/cctm/CCTM_d1bCONC.d1b
#    setenv MET_CRO_3D_CRS
#    setenv MET_CRO_3D_FIN
#    setenv SDATE           ${DATE}
#    setenv STIME           000000
#    setenv RUNLEN          240000
 endif

# =====================================================================
#> Output Files
# =====================================================================
 
 if ( $BC == profile ) then
    setenv BNDY_CONC_1    "$OUTDIR/BCON_${VRSN}_profile -v"
    endif
 if ( $BC == m3conc ) then 
    set DATE = ${YYYYMMDD}  # July 1, 2011
    setenv BNDY_CONC_1    "$OUTDIR/BCON_${VRSN}_${YYYYMMDD} -v"
 endif

#>- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

#> species defn
 setenv gc_matrix_nml ${BLD}/GC_$MECH.nml
 setenv ae_matrix_nml ${BLD}/AE_$MECH.nml
 setenv nr_matrix_nml ${BLD}/NR_$MECH.nml
 setenv tr_matrix_nml ${BLD}/Species_Table_TR_0.nml
 
 if ( ! -d "$OUTDIR" ) mkdir -p $OUTDIR

 ls -l $BLD/$EXEC; size $BLD/$EXEC
 unlimit
 limit

#> Executable call:
 time $BLD/$EXEC

 echo "run exe finished"
 
 set TODAYG = `date -ud "${TODAYG}+1days" +%Y-%m-%d` #> Add a day for tomorrow
 set TODAYJ = `date -ud "${TODAYG}" +%Y%j` #> Convert YYYY-MM-DD to YYYYJJJ

end

exit
